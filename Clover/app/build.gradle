apply plugin: 'android'

android {
    signingConfigs {
    }
    compileSdkVersion 19
    buildToolsVersion '21.0.2'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        versionName "0.9-jp"
        versionCode 44
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    /*
    If you want to sign releases, make a file in app/keys.properties with the following content:
    keystoreFile=yourkey.store
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */
    File f = file('keys.properties')
    boolean doSign = f.exists();
    if (doSign) {
        Properties props = new Properties()
        props.load(new FileInputStream(f))

        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }
    }
    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            runProguard true
            proguardFiles 'proguard.cfg'
        }
        beta.initWith(buildTypes.release)
        beta {
            versionNameSuffix ' Beta'
        }
        debug {
            versionNameSuffix ' Debug'
        }
    }
    sourceSets {
        beta.java.srcDirs = ['src/release/java']
    }
    productFlavors {
    }
}

dependencies {
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.android.support:support-v13:19.1.+'
    compile 'com.koushikdutta.ion:ion:1.+'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.0.+'
}
